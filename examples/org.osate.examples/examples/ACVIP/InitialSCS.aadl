-- Copyright 2022 Carnegie Mellon University.
-- This material is based upon work funded and supported by the Department of Defense
-- under Contract No. FA8702-15-D-0002 with Carnegie Mellon University for the operation of the 
-- Software Engineering Institute, a federally funded research and development center.
-- The view, opinions, and/or findings contained in this material are those of the author(s)
-- and should not be construed as an official Government position, policy, or decision,
-- unless designated by other documentation.
--
-- THIS CARNEGIE MELLON UNIVERSITY AND SOFTWARE ENGINEERING INSTITUTE MATERIAL IS FURNISHED ON AN "AS-IS" BASIS.
-- CARNEGIE MELLON UNIVERSITY MAKES NO WARRANTIES OF ANY KIND, EITHER EXPRESSED OR IMPLIED, AS TO ANY MATTER INCLUDING,
-- BUT NOT LIMITED TO, WARRANTY OF FITNESS FOR PURPOSE OR MERCHANTABILITY, EXCLUSIVITY, OR RESULTS OBTAINED
-- FROM USE OF THE MATERIAL. CARNEGIE MELLON UNIVERSITY DOES NOT MAKE ANY WARRANTY OF ANY KIND WITH RESPECT TO FREEDOM
-- FROM PATENT, TRADEMARK, OR COPYRIGHT INFRINGEMENT.
-- [DISTRIBUTION STATEMENT A] This material has been approved for public release and unlimited distribution.
--  Please see Copyright notice for non-US Government use and distribution.
--
-- Internal use:* Permission to reproduce this material and to prepare derivative works from this material
-- for internal use is granted, provided the copyright and “No Warranty” statements are included with all
-- reproductions and derivative works.
--
-- External use:* This material may be reproduced in its entirety, without modification, and freely distributed
-- in written or electronic form without requesting formal permission. Permission is required for any other external
-- and/or commercial use. Requests for permission should be directed to the Software Engineering Institute
-- at permission@sei.cmu.edu.
-- These restrictions do not apply to U.S. government entities.
-- DM22-0018

package InitialSCS
public
	system HighLevelSCS		
	end HighLevelSCS;

	system implementation HighLevelSCS.i
		subcomponents
			sensor1: device SimpleSensor.i;
			controller1: system SimpleController.i;
			brake1: device SimpleBrake.i;
		connections
			c1: port sensor1.dataOut -> controller1.dataIn;
			c2: port controller1.dataOut -> brake1.dataIn;
		flows
			ete1: end to end flow sensor1.sensorFlow ->c1 -> controller1.controllerFlow -> c2 -> brake1.brakeFlow;  
	end HighLevelSCS.i;
	
	data SensorData
	end SensorData;
	
	data BrakeSignal
	end BrakeSignal;
	
	device SimpleSensor
		features
			dataOut: out data port SensorData;
		flows
			sensorFlow: flow source dataOut;
	end SimpleSensor;
	
	device implementation SimpleSensor.i
		properties
			latency => 3ms..5ms applies to sensorFlow;                  
	end SimpleSensor.i;

	system SimpleController
		features
			dataIn: in data port SensorData;
			dataOut: out data port BrakeSignal;
		flows
			controllerFlow: flow path dataIn -> dataOut;
	end SimpleController;
	
	system implementation SimpleController.i
		properties
			latency => 3ms..5ms  applies to controllerFlow;
	end SimpleController.i;
	
	device SimpleBrake
		features
			dataIn: in data port BrakeSignal;
		flows
			brakeFlow: flow sink dataIn;
	end SimpleBrake;
	
	device implementation SimpleBrake.i
		properties
			latency => 3ms..5ms applies to brakeFlow; 
	end SimpleBrake.i;	
end InitialSCS;