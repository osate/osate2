--EMV2.aadl

property set EMV2
  is
  
OccurrenceDistribution : EMV2::DistributionSpecification
	  applies to ({emv2}**error propagation, {emv2}**error flow, {emv2}**error behavior event
	  ,{emv2}**error behavior state,{emv2}**error type,{emv2}**type set);
   

--needed to separate the property "Occurrence" from its type declaration
--so other properties may have that type
DistributionSpecification : type record (
	ProbabilityValue : aadlreal;
	OccurrenceRate : aadlreal;
	MeanValue : aadlreal;
	StandardDeviation : aadlreal;
	ShapeParameter : aadlreal;
	ScaleParameter : aadlreal;
	SuccessCount : aadlreal;
	SampleCount : aadlreal;
	Probability : aadlreal;
	Distribution : EMV2::DistributionFunction;);

--	Fixed represents a fixed distribution and takes a single parameter OccurrenceRate or ProbabilityValue. 
--	Poisson aka. Exponential represents an exponential distribution and takes a single parameter OccurrenceRate or ProbabilityValue.
--	Normal aka. Gauss represents a distribution with an explicitly specified MeanValue and StandardDeviation. 
--	Weibull represents a shaped distribution with a ShapeParameter and a ScaleParameter.
--	Binominal represents represents a discrete distribution with a SuccessCount, a SampleCount, and a Probability parameter.
	
DistributionFunction : type enumeration (Fixed, Poisson, Exponential, Normal, Gauss, Weibull, Binominal);
  
DurationDistributionSpecification : type record (
	Duration : Time_Range;
	Distribution : EMV2::DistributionFunction;); 
	  
DurationDistribution : EMV2::DurationDistributionSpecification applies to ({emv2}**Repair Event, {emv2}**Recover Event);

-- A PropagationTimeDelay property indicates the delay in propagating and error as a distribution over a time interval. 
-- For example, as a property associated with a connection it indicates the time delay of the error propagation.

PropagationTimeDelay: EMV2::DurationDistributionSpecification
    applies to (connection, {emv2}**propagation point connection);

StateKind : EMV2::StateKindEnum
	  applies to ({emv2}**error behavior state);

StateKindEnum: type enumeration (Working, NonWorking);

-- Indicates whether an error type is detectable
DetectionMechanism : aadlstring
	  applies to ({emv2}**error detection); 
 

-- The FaultKind property allows the user to specify whether an error source, 
-- i.e., the occurrence of a fault activation or a propagation is due to a design fault or an operational fault. 
-- Design faults are faults that could be eliminated at design time, but if present result in an error. 
-- Operational faults are faults that inherently occur during operation and should be detected and managed during operation.
FaultKind : EMV2::FaultKindEnum
	  applies to ( {emv2}**error event, {emv2}**error propagation, {emv2}**error source, {emv2}**error type, {emv2}**type set); 

FaultKindEnum: type enumeration (Design, Operational);


-- The Persistence property allows the user to specify whether an error is permanent, transient, or a singleton. 
-- A permanent error typically requires a repair action to the component with the fault occurrence. 
-- A transient error has a limited duration and typically requires a recovery action. 
-- In a discrete event system a transient error may last over several discrete events, e.g., a corrupted message may be sent over multiple periods. 
-- A singleton error occurs in the context of a single discrete event. For example, a divide by zero error may be specific to a computation on a particular input.
Persistence : EMV2::PersistenceEnum
	  applies to ({emv2}**error type, {emv2}**type set, {emv2}**error behavior state, 
	  {emv2}**error behavior event, {emv2}**error propagation);

PersistenceEnum: type enumeration (Permanent, Transient, Singleton);

-- The severity property value indicates the severity of the hazard ranging from 1 (high) to 5 (low). 
-- MIL-STD 882D suggests descriptive labels (Catastrophic, Critical, Marginal, Negligible) (See property set MILSTD882). 
-- ARP 4761 defines descriptive labels (Catastrophic, Hazardous, Major, Minor, NoEffect) (See property set ARP4761).
Severity : inherit aadlinteger applies to ({emv2}**error type, {emv2}**type set, {emv2}**error behavior state, 
	  {emv2}**error source, {emv2}**error propagation, {emv2}**error event);

-- A likelihood property value indicates the likelihood with which the hazard occurs. 
-- Likelihood is expressed in terms of levels ranging from A (high) to E (low). 
-- Each level typically has an associated probability of occurrence (p) threshold. 
-- MIL-STD 882D suggests likelihood levels (Frequent: p>10e-1, Probable: 10e-1 > p > 10e-2, Occasional: 10e-2 > p > 10e-3, Remote: 10e-3 > p > 10e-6, Improbable: p < 10e-6) for probability of occurrence over the life of an item. 
-- In the context of ARP 4761 or DO178 defines descriptive labels (Probable: p > 10e-5, Remote: 10e-5 > p > 10e-7, ExtremelyRemote: 10e-7 < p > 10e-9, ExtremelyImprobable: p < 10e-9) for probability of occurrence per operational hour.
Likelihood : inherit enumeration (A, B, C, D, E) applies to ({emv2}**error type, {emv2}**type set, {emv2}**error behavior state, 
	  {emv2}**error source, {emv2}**error propagation, {emv2}**error event);

Hazard: record
  (crossreference: aadlstring;
  	failure : aadlstring;
  	phase : aadlstring;
  	environment : aadlstring;
  	description : aadlstring;
  	verificationmethod : aadlstring;
  	risk : aadlstring;
  	comment : aadlstring;) applies to ({emv2}**error type, {emv2}**type set, {emv2}**error behavior state, 
	  {emv2}**error source, {emv2}**error propagation, {emv2}**error event);

Description : aadlstring applies to (all); 


end EMV2;
