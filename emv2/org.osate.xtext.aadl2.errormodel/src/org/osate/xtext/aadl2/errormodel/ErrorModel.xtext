grammar org.osate.xtext.aadl2.errormodel.ErrorModel 
with org.osate.xtext.aadl2.properties.Properties

generate errorModel "http://www.aadl.info/EMV2"


import "platform:/resource/org.osate.aadl2/model/aadl2.ecore" as aadl2

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

/*
 * Note about symbolic labels as values for occurrence probability:
 * Use property constant. The core allows it instead of an actual number.
 * The tools generating stochastic models from such specification can interpret the constant name as the desired label.
 */

// allow either of the two to be the root. Needed dummy 'library' keyword

ErrorModelGrammarRoot : 	 'library' eml=ErrorModelLibrary  
	| emsc=ErrorModelSubclause
;

AnnexLibrary returns aadl2::AnnexLibrary:
	 ErrorModelLibrary 
	 ;


AnnexSubclause returns aadl2::AnnexSubclause:
	 ErrorModelSubclause 
	 ;

NamedElement returns aadl2::NamedElement:
ErrorModelLibrary| 
ErrorTypes| ErrorBehaviorEvent | ErrorBehaviorState | ErrorBehaviorTransition
	   | ErrorFlow| ErrorPropagation |OutgoingPropagationCondition
|PropagationPointConnection | PropagationPoint |CompositeState
	|TypeTransformationSet | TypeMappingSet  | ErrorBehaviorStateMachine
|ErrorDetection	|  ConnectionErrorSource | EventOrPropagation
;

ModalElement returns aadl2::ModalElement:
ErrorModelSubclause 
;

Element returns aadl2::Element:
TypeSetElement|TypeToken 
|TypeTransformation|TypeMapping
|QualifiedPropagationPoint|TransitionBranch
|ConditionElement
|ConditionExpression|OrmoreExpression|OrlessExpression|OutgoingPropagationCondition
	|ErrorStateToModeMapping
| SubcomponentElement | FeatureorPPReference  | TypeUseContext
;


ErrorModelSubclause returns ErrorModelSubclause:
     {ErrorModelSubclause}//'{**' 
   ('use' 'types' useTypes+=[ErrorModelLibrary|QEMREF] (',' useTypes+=[ErrorModelLibrary|QEMREF] )* ';')?
   ('use' 'type' 'equivalence' typeEquivalence=[TypeMappingSet|QEMREF] ';')?
   ('use' 'behavior' useBehavior=[ErrorBehaviorStateMachine|QEMREF] ';' )?
  (    'error' 'propagations'
   (propagations+=ErrorPropagation)* 
   ( 'flows'
   (flows+=ErrorFlow)+)? 
   'end' 'propagations' ';'
  )?
  (
  		'component' 'error' 'behavior' 
   ('use' 'transformations' useTransformation=[TypeTransformationSet|QEMREF] ';')?
  ( 'events' (events+=ErrorBehaviorEvent )+ )?
   (  'transitions' 
    ( transitions+=ErrorBehaviorTransition )+ )?
   (  'propagations'
    ( outgoingPropagationConditions+=OutgoingPropagationCondition )+ )?
   (  'detections'
    ( errorDetections+=ErrorDetection )+ )?
   (  'mode' 'mappings'
    ( errorStateToModeMappings+=ErrorStateToModeMapping )+ )?
   'end' 'component' ';'
  	
  )?
  (
	'composite' 'error' 'behavior' 
   ( 'states' 
    ( states+=CompositeState )+ 
    )?
   'end' 'composite' ';'
  )?
  (
  		'connection' 'error' 
	('use' 'transformations' typeTransformationSet=[TypeTransformationSet|QEMREF] ';')?
	( connectionErrorSources+= ConnectionErrorSource )*
	'end' 'connection' ';'
  	
  )?
  (
  		'propagation' 'paths' 
	( points+=PropagationPoint)+
	('connections'
	( connections+=PropagationPointConnection)+
	)?
	'end' 'paths' ';'
  )?
    ('properties'
 	(properties+=ContainedPropertyAssociation )+)?
//	'**}'
	 ;

ErrorModelLibrary returns ErrorModelLibrary:
	{ErrorModelLibrary}
  //    '{**' 
	(
    'error' 'types' 
    ('extends' extends+=[ErrorModelLibrary|QEMREF] (','extends+=[ErrorModelLibrary|QEMREF])*
    'with')?     
(((types+=TypeDefinition)|(typesets+=TypeSetDefinition)))*
    ('properties'
 	(properties+=ContainedPropertyAssociation )+)?
     'end' 'types' ';'
	)?
    (behaviors+=ErrorBehaviorStateMachine )*
	(mappings+=TypeMappingSet )* 
	(transformations+=TypeTransformationSet )*
//	'**}'
	 ;
	

ErrorTypes returns ErrorTypes:
	TypeDefinition | TypeSetDefinition 
;

TypeDefinition returns ErrorType: 
    name=ID (
    	(':' 'type' ( 'extends' superType=[ErrorType|QEMREF] )? )
    	|('renames' 'type' aliasedType=[ErrorType|QEMREF])
    	)
    ';';



TypeSetDefinition returns TypeSet: 
     name=ID (
     (':' 'type' 'set' '{' typeTokens+=TypeSetElement (',' typeTokens+=TypeSetElement)* '}')
     |('renames' 'type' 'set' aliasedType=[TypeSet|QEMREF])
     )
          ';';


TypeSetConstructor returns TypeSet: 
     '{' typeTokens+=TypeSetElement (',' typeTokens+=TypeSetElement)* '}'
          ;

TypeSetReference returns TypeSet: 
     TypeSetConstructor | TypeSetID
          ;

TypeSetID returns TypeSet:
	typeTokens+=TypeSetAsElement	
;
      
TypeSetAsElement returns TypeToken :
	type+=[TypeSet|QEMREF] 
;
      
TypeSetElement returns TypeToken :
	type+=[ErrorTypes|QEMREF] (('*') type+=[ErrorType|QEMREF])*
;

NoErrorTypeSet returns TypeSet:
	{TypeSet}
	'{' typeTokens+=NoErrorTypeToken '}'
;

NoErrorTypeToken returns TypeToken:
	{TypeToken}
	noError?='noerror'
;

TypeToken returns TypeToken:
	{TypeToken}
     '(' type+=[ErrorType|QEMREF] (('*') type+=[ErrorType|QEMREF])* ')'
;

TypeTokenOrNoError returns TypeToken:
	TypeToken | '(' NoErrorTypeToken ')'
;

TypeTokenConstraint returns TypeSet:
	TypeSetReference
;

TypeTokenConstraintNoError returns TypeSet:
	TypeSetReference | NoErrorTypeSet
;

TypeTransformationSet returns TypeTransformationSet: 
	'type' 'transformations' name=ID 
   ('use' 'types' useTypes+=[ErrorModelLibrary|QEMREF] (',' useTypes+=[ErrorModelLibrary|QEMREF] )* ';')?
      ( transformation+=TypeTransformation )+ 
      'end' 'transformations' ';'
          ;

TypeTransformation returns TypeTransformation: 
	(source=TypeTokenConstraintNoError |allSources?='all')
	'-[' (contributor=TypeTokenConstraintNoError)? ']->' target=TypeToken ';'
          ;


TypeMappingSet returns TypeMappingSet:
	'type' 'mappings' name=ID
   ('use' 'types' useTypes+=[ErrorModelLibrary|QEMREF] (',' useTypes+=[ErrorModelLibrary|QEMREF] )* ';')?
	(mapping+=TypeMapping)+
	'end' 'mappings' ';'
;

TypeMapping returns TypeMapping:
	 source=TypeTokenConstraint '->' target=TypeToken ';'
;

   
ErrorPropagation returns ErrorPropagation:
	{ErrorPropagation}
   (  kind=PropagationKind | (featureorPPRefs+=FeatureorPPReference)("." (featureorPPRefs+=FeatureorPPReference))* )
   ':' (not?='not')? (direction=PropagationDirection)  
   'propagation' 
    typeSet=TypeSetReference
   ';';

FeatureorPPReference returns FeatureorPPReference:
	featureorPP=[aadl2::NamedElement|ID]
;
   
PropagationDirection returns aadl2::DirectionType: 'in' | 'out' ;

PropagationKind  : ('processor'|'memory'|'connection' | 'bindings'|'access'); 

// enum does not work as  
//enum PropKind : PROCESSOR='processor'|MEMORY='memory'|BUS='bus'|DEVICE='device'|SYSTEM='system'
//	            |VIRTUALBUS='virtual bus'|VIRTUALPROCESSOR='virtual processor'|BINDING='binding' | BINDINGS='bindings'|ACCESS='access'
//;

  ErrorFlow returns ErrorFlow:
  ErrorSource | ErrorSink | ErrorPath; 

ErrorSource returns ErrorSource:
  name=ID ':' 'error' 'source' (outgoing=[ErrorPropagation|ErrorPropagationPoint]
  	|allOutgoing?='all'
  ) (typeTokenConstraint=TypeTokenConstraint)?
  	('when' (( failureModeReference = [ErrorBehaviorStateOrTypeSet|ID] ( failureModeType = TypeSetReference )?) 
  	| ( failureModeType = TypeSetConstructor )	 | failureModeDescription = STRING
  	) )?
  ';';
 
ErrorBehaviorStateOrTypeSet: ErrorBehaviorState | TypeSetDefinition;

ErrorSink returns ErrorSink:
  name=ID ':' 'error' 'sink' (incoming=[ErrorPropagation|ErrorPropagationPoint]|allIncoming?='all') (typeTokenConstraint=TypeTokenConstraint)?
  ';';

ErrorPath returns ErrorPath:
  name=ID ':' 'error' 'path' (incoming=[ErrorPropagation|ErrorPropagationPoint]|allIncoming?='all') ( typeTokenConstraint=TypeTokenConstraint)? '->' (outgoing=[ErrorPropagation|ErrorPropagationPoint]|allOutgoing?='all') 
  ( targetToken=TypeToken | 'use' 'mappings' typeMappingSet=[TypeMappingSet|QEMREF])?
  ';';

// reference to error propagation  
ErrorPropagationPoint :
   PropagationKind | (ID  ('.' ID)*)
   ;

   
PropagationPoint returns PropagationPoint:
    name=ID ':' 'propagation' 'point'  
   ';';
   
PropagationPointConnection returns PropagationPointConnection:
   	name=ID ':' source=QualifiedPropagationPoint '->' target=QualifiedPropagationPoint ';'
   ;
  
QualifiedPropagationPoint returns QualifiedPropagationPoint:
   (subcomponent=[aadl2::Subcomponent|ID]  '.')? propagationPoint=[PropagationPoint|ID]
   ;
   


ErrorBehaviorStateMachine returns ErrorBehaviorStateMachine: 
  'error' 'behavior' name=ID
   ('use' 'types' useTypes+=[ErrorModelLibrary|QEMREF] (',' useTypes+=[ErrorModelLibrary|QEMREF] )* ';')?
   ('use' 'transformations' useTransformation+=[TypeTransformationSet|QEMREF] ';')?
  ( 'events' (events+=ErrorBehaviorEvent )+ )?
  ( 'states' (states+=ErrorBehaviorState )+ )?
  ( 'transitions' (transitions+=ErrorBehaviorTransition )+ )?
    ('properties'
 	(properties+=ContainedPropertyAssociation )+)?
  'end' 'behavior' ';';
  
ErrorBehaviorEvent returns ErrorBehaviorEvent: 
  ErrorEvent | RepairEvent | RecoverEvent;
  
ErrorEvent returns ErrorEvent:
    name=ID ':' 'error' 'event' 
    (typeSet=TypeSetReference)?
    ('when' condition=STRING)?
    ';'; // add event condition 
    
RepairEvent returns RepairEvent:
    name=ID ':' 'repair' 'event' 
    ('when' condition+=[aadl2::NamedElement|ID]
    	( "," condition+=[aadl2::NamedElement|ID])*
    )?
    ';';  
    
RecoverEvent returns RecoverEvent:
    name=ID ':' 'recover' 'event' 
    ('when' condition+=[aadl2::NamedElement|ID]
    	( "," condition+=[aadl2::NamedElement|ID])*
    )?
    ';'; //add recover initiator 

ErrorBehaviorState returns ErrorBehaviorState: 
    name=ID ':' (intial?='initial')? 'state'
    (typeSet=TypeSetReference)?
    ';'  ;

ErrorBehaviorTransition returns ErrorBehaviorTransition:
  (name = ID ':')? 
  ((source=[ErrorBehaviorState|ID] (typeTokenConstraint=TypeTokenConstraint)?)
  	| allStates?='all')
   '-[' condition=ConditionExpression ']->' 
  ((target=[ErrorBehaviorState|ID] (targetToken=TypeToken )?)| (steadyState ?='same' 'state')
    	| ('('destinationBranches+=TransitionBranch (',' destinationBranches+=TransitionBranch )+ ')'))
  ';';

TransitionBranch returns TransitionBranch:  
	((target=[ErrorBehaviorState|ID] (targetToken=TypeToken )?) 
	| (steadyState ?='same' 'state'))
	'with' value=BranchValue 
	
;
// store real literal as integer, ID as reference to property constant
BranchValue returns BranchValue: 
	{BranchValue}
   ((realvalue = REAL_LIT) | (symboliclabel = [aadl2::PropertyConstant|QEMREF]) | (others?='others'));


ConnectionErrorSource returns ConnectionErrorSource:
	name=ID ':' 'error' 'source' 
	(connection=[aadl2::Connection|ID]|all?='all')
	(typeTokenConstraint=TypeTokenConstraint)?
  	('when'  (  failureModeType = TypeSetConstructor | failureModeDescription = STRING)
  	)?
  	';'
;

TypeUseContext returns TypeUseContext:
	TypeTransformationSet | TypeMappingSet |  ErrorBehaviorStateMachine  
	| ErrorModelSubclause
;

 
 
 // condition expression for component specific transitions 
 // based on error events and incoming propagations
ConditionExpression returns ConditionExpression:
  AndExpression ( {OrExpression.operands+=current} 'or' operands+=AndExpression)*;


AndExpression returns ConditionExpression:
  ConditionTerm ({AndExpression.operands+=current} 'and' operands+=ConditionTerm)*;
  
OrmoreExpression returns OrmoreExpression: 
	count=INTVALUE 'ormore' '(' operands+=ConditionElement
	            ( ',' operands+=ConditionElement)* ')'
;
  
OrlessExpression returns OrlessExpression: 
	count=INTVALUE 'orless' '(' operands+=ConditionElement
	            ( ',' operands+=ConditionElement)* ')'
;
  
ConditionTerm returns ConditionExpression:
	  ConditionElement
      |  OrmoreExpression 
      |  OrlessExpression 
      | '(' ConditionExpression ')'
  ;
  
ConditionElement returns ConditionElement:
	// ( subcomponent=[Subcomponent|ID] '.' )* or ?
  incoming=[EventOrPropagation|ErrorPropagationPoint] ( constraint=TypeTokenConstraintNoError )?
  ;

EventOrPropagation :
	ErrorBehaviorEvent | ErrorPropagation
;



OutgoingPropagationCondition returns OutgoingPropagationCondition: 
    (name = ID ':')? 
    ((state=[ErrorBehaviorState|ID] (typeTokenConstraint=TypeTokenConstraint)?)
    	| allStates?='all'
    )
	'-[' (condition=ConditionExpression)? ']->' 
	((outgoing=[ErrorPropagation|ErrorPropagationPoint]|allPropagations?='all' ) (typeToken=TypeTokenOrNoError)?)
	 ';'
;

  

ErrorDetection returns ErrorDetection: 
    (name = ID ':' )?
    ((state=[ErrorBehaviorState|ID] (typeTokenConstraint=TypeTokenConstraint)?)|
    	allStates?='all'
    )
	'-[' (condition=ConditionExpression)? ']->' 
	( internalDetectionPort=InternalPort| detectionReportingPort=[aadl2::Port|ID] ) '!'
	(errorCode=ErrorCodeValue)?
	';' 
;

ErrorCodeValue returns ErrorCodeValue:
	intValue=INTEGER_LIT | constant=[aadl2::PropertyConstant|QPREF] | enumLiteral=STRING
;

ErrorStateToModeMapping returns ErrorStateToModeMapping:
	errorState=[ErrorBehaviorState|ID] (typeToken=TypeToken)?
	'in' 'modes' '('
	mappedModes+=[aadl2::Mode|ID] (',' mappedModes+=[aadl2::Mode|ID] )* ')' ';'
;


InternalPort returns aadl2::InternalEvent:
	 'self' '.' name=ID
;

CompositeState returns CompositeState:
    (name = ID ':' )?
	'[' (condition=SConditionExpression | others?='others') ']->' state=[ErrorBehaviorState|ID] (typedToken=TypeToken)?';';

  
// condition expressions for composite states
// based on states only  
SConditionExpression returns ConditionExpression:
  SAndExpression ( {SOrExpression.operands+=current} 'or' operands+=SAndExpression)*;


SAndExpression returns ConditionExpression:
  SConditionTerm ({SAndExpression.operands+=current} 'and' operands+=SConditionTerm)*;
  
SOrmoreExpression returns OrmoreExpression: 
	count=INTVALUE 'ormore' '(' operands+=SConditionElement
	            ( ',' operands+=SConditionElement)* ')'
;
  
SOrlessExpression returns OrlessExpression: 
	count=INTVALUE 'orless' '(' operands+=SConditionElement
	            ( ',' operands+=SConditionElement)* ')'
;
  
SConditionTerm returns ConditionExpression:
	  SConditionElement
      |  SOrmoreExpression 
      |  SOrlessExpression 
      | '(' SConditionExpression ')'
  ;

  
SConditionElement returns ConditionElement:
	((subcomponents+=SubcomponentElement '.')+ state=[ErrorBehaviorState|ID] (constraint=TypeTokenConstraint)?)
	|
  	'in' (incoming=[ErrorPropagation|ErrorPropagationPoint] ( constraint=TypeTokenConstraintNoError )?)
  ;

SubcomponentElement returns SubcomponentElement:
	subcomponent = [aadl2::Subcomponent|ID]
;


terminal SL_COMMENT:
	'--' !('\n' | '\r')* ('\r'? '\n')?;



terminal INTEGER_LIT : ('0'..'9')+;


QEMREF:
	 (ID '::')* ID ;//('.' ID )?;


