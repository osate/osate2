grammar org.osate.reqspec.ReqSpec with org.osate.alisa.common.Common

generate reqSpec "http://www.osate.org/reqspec/ReqSpec"

import "http://aadl.info/AADL/2.0" as aadl2
import  "http://www.osate.org/categories/Categories" as categories
import  "http://www.osate.org/organization/Organization" as org
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

ReqSpec: parts +=(SystemRequirements|StakeholderGoals|ReqDocument|GlobalConstants)+  ;

ReqSpecContainer: SystemRequirements |  StakeholderGoals | ReqDocument | DocumentSection;

GlobalConstants: 
	'constants' name=QualifiedName '['
	constants+=ValDeclaration* ']'
;

ContractualElement: Goal | DocGoal| Requirement |DocRequirement ;

ReqRoot:  StakeholderGoals | ReqDocument | SystemRequirements;


StakeholderGoals: 'stakeholder' 'goals' name=QualifiedName (':' title=STRING)?
	( 'for' (target=[aadl2::ComponentClassifier|AadlClassifierReference] | category+=[categories::ComponentCategory|ID]+ | global?='all'))
	('use' 'constants' importConstants+=[GlobalConstants|QualifiedName]+)?
	'[' 
	(	description=Description? 
		& constants+=ValDeclaration*
		&content+=Goal* 
	    &('see' 'document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
				)
	']'
;

ReqDocument: 'document' name=QualifiedName (':' title=STRING)?
	'[' 
	( (  description=Description)? 
	&(content+= (DocGoal | DocRequirement |  DocumentSection))*
	&('issues' issues+=STRING+ )?
	)
	 ']'
;
DocumentSection returns DocumentSection:
	'section' label =ID (':' title=STRING)?
	'[' (
	 (  description=Description)? 
	
	&(content+= (DocGoal | DocRequirement |  DocumentSection))*
	)
	']' 
	;

SystemRequirements returns SystemRequirements:
	'system' 'requirements'  name =QualifiedName (':' title=STRING)? // 'requirement' 'specification'
	( 'for' (target=[aadl2::ComponentClassifier|AadlClassifierReference] | category+=[categories::ComponentCategory|ID]+| global?='all'))
	('use' 'constants' importConstants+=[GlobalConstants|QualifiedName]+)?
	'[' 
	(
	 description=Description? 
	&constants+=ValDeclaration*
	& computes+=ComputeDeclaration* 
	& content+= (Requirement)*
	 &('see' 'document'  docReference+=ExternalDocument+)? 
	&('issues' issues+=STRING+ )?
	)
	']'
	;


	

DocGoal returns Goal:
	'goal' name=ID (':'  title=STRING )? // short string for title
	( 'for' ( targetDescription=STRING | (target=[aadl2::ComponentClassifier|AadlClassifierReference] ( targetElement=[aadl2::NamedElement|ID])?  )
		)
	)?
'['  (
	 ('category'  category+=[categories::Category|ID]+)?
	 & description=Description? 
	&constants+=ValDeclaration* 
	 &rationale=Rationale? 
	 &changeUncertainty=Uncertainty? 
	 &('refines' refinesReference+=[Goal|QualifiedName]+)? 
	 &('conflicts' 'with' conflictsReference+=[Goal|QualifiedName]+)? 
	&('evolves' evolvesReference+=[Requirement|QualifiedName]+)? 
	&(dropped?='dropped' ( dropRationale=STRING )? )? 
	 &('stakeholder'  stakeholderReference+=[org::Stakeholder|QualifiedName]+)?
	 &('see' 'document' 'requirement'  documentRequirement+=[ContractualElement|QualifiedName]+)?
	 &('see' 'document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	 ']';


Goal returns Goal:
	'goal' name=ID (':'  title=STRING )? // short string for title
	( 'for'  targetElement=[aadl2::NamedElement|ID]
	)?
'['  (
	 ('category'  category+=([categories::Category|ID])+)?
	 &(  description=Description)? 
	& (  constants+=ValDeclaration* )
	 &(rationale=Rationale)? 
	 &(changeUncertainty=Uncertainty)? 
	 &('refines' refinesReference+=[Goal|QualifiedName]+)? 
//	 &('refined' 'to' '['refinedGoal+=Goal+ ']')? 
	 &('conflicts' 'with' conflictsReference+=[Goal|QualifiedName]+)? 
	&('evolves' evolvesReference+=[Requirement|QualifiedName]+)? 
	&(dropped?='dropped' ( dropRationale=STRING )? )? 
	 &('stakeholder'  stakeholderReference+=[org::Stakeholder|QualifiedName]+)?
	 &('see' 'document' 'requirement'  documentRequirement+=[ContractualElement|QualifiedName]+)?
	 &('see' 'document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	 ']';

// requirement without target acts as a general requirement/constraint
// becomes a "global" constraint that still takes model elements that are supplied by query 	
Requirement returns Requirement:
	'requirement'  name=ID   
	(':'  title=STRING)? 
	( 'for'  targetElement=[aadl2::NamedElement|ID]
	)?
'['	(
 	('category'  category+=[categories::Category|ID]+ )?
	&(  description=Description)? 
	& (  constants+=ValDeclaration* )
	& (  computes+=ComputeDeclaration* )
	&(  predicate=ReqPredicate)?  // predicate expression 
	&(rationale=Rationale)? 
	 &(changeUncertainty=Uncertainty)? 
	&('mitigates'  (exception=[ecore::EObject|ID]|exceptionText=STRING))? 
	&('refines' refinesReference+=[Requirement|QualifiedName]+)? 
//	&('refined' 'to' '['refinedRequirement+=Requirement+ ']')? 
	&('decomposes' decomposesReference+=[Requirement|QualifiedName]+)? 
	&('evolves' evolvesReference+=[Requirement|QualifiedName]+)? 
	&(dropped?='dropped' ( dropRationale=STRING )? )? 
	 &('development' 'stakeholder'  developmentStakeholder+=[org::Stakeholder|QualifiedName]+)?
	&('see' 'goal'  goalReference+=[Goal|QualifiedName]+)?
	&('see' 'document' 'goal'  stakeholderRequirementReference+=[Goal|QualifiedName]+)?
	 &('see' 'document' 'requirement'  documentRequirement+=[ContractualElement|QualifiedName]+)?
	 &('see' 'document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	']';
	
// requirement without target acts as a general requirement/constraint
// becomes a "global" constraint that still takes model elements that are supplied by query 	
DocRequirement returns Requirement:
	'requirement'  name=ID   
	(':'  title=STRING)? 
	( 'for' ( targetDescription=STRING | (target=[aadl2::ComponentClassifier|AadlClassifierReference] ( targetElement=[aadl2::NamedElement|ID])?  )
		)
	)?
'['	(
 	('category'  category+=[categories::Category|ID]+ )?
	&(  description=Description)? 
	& (  constants+=ValDeclaration* )
	& (  computes+=ComputeDeclaration* )
	&(  predicate=ReqPredicate)?  // predicate expression 
	&(rationale=Rationale)? 
	 &(changeUncertainty=Uncertainty)? 
	&('mitigates'  (exception=[ecore::EObject|ID]|exceptionText=STRING))? 
	&('refines' refinesReference+=[Requirement|QualifiedName]+)? 
	&('decomposes' decomposesReference+=[Requirement|QualifiedName]+)? 
	&('evolves' evolvesReference+=[Requirement|QualifiedName]+)? 
	&(dropped?='dropped' ( dropRationale=STRING )? )? 
	 &('development' 'stakeholder'  developmentStakeholder+=[org::Stakeholder|QualifiedName]+)?
	&('see' 'goal'  goalReference+=[Goal|QualifiedName]+)?
	&('see' 'document' 'goal'  stakeholderRequirementReference+=[Goal|QualifiedName]+)?
	 &('see' 'document' 'requirement'  documentRequirement+=[ContractualElement|QualifiedName]+)?
	 &('see' 'document'  docReference+=ExternalDocument+)? 
		&('issues' issues+=STRING+ )?
	)
	']';

ReqPredicate: InformalPredicate | XPredicate | ValuePredicate;// | InputAssumption | OutputGuarantee| BehaviorEquation;

InformalPredicate: 'informal' 'predicate'  description=STRING ;
XPredicate: 'predicate' xpression=AExpression ;

ValuePredicate: 'value' 'predicate' xpression=AAndExpression;	// intended to be restricted to deal with value limits



//InputAssumption:  'input' 'assumption' (xpression=XExpression |'as' reference=[ecore::EObject|ID]);	
//OutputGuarantee: 'output''guarantee' (xpression=XExpression |'as' reference=[ecore::EObject|ID]);	
//BehaviorEquation: 'invariant' (xpression=XExpression |'as' reference=[ecore::EObject|ID]);

ExternalDocument : 
	docReference=DOCPATH ( '#' docFragment=DOCFRAGMENT)?
;


// path to external document and its fragment
DOCPATH: ( ID '/')* ID '.' ID;

DOCFRAGMENT: ID ('.' ID)*;

	