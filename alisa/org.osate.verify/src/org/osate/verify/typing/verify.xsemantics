/**
 * Copyright (c) 2004-2020 Carnegie Mellon University and others. (see Contributors file). 
 * All Rights Reserved.
 * 
 * NO WARRANTY. ALL MATERIAL IS FURNISHED ON AN "AS-IS" BASIS. CARNEGIE MELLON UNIVERSITY MAKES NO WARRANTIES OF ANY
 * KIND, EITHER EXPRESSED OR IMPLIED, AS TO ANY MATTER INCLUDING, BUT NOT LIMITED TO, WARRANTY OF FITNESS FOR PURPOSE
 * OR MERCHANTABILITY, EXCLUSIVITY, OR RESULTS OBTAINED FROM USE OF THE MATERIAL. CARNEGIE MELLON UNIVERSITY DOES NOT
 * MAKE ANY WARRANTY OF ANY KIND WITH RESPECT TO FREEDOM FROM PATENT, TRADEMARK, OR COPYRIGHT INFRINGEMENT.
 * 
 * This program and the accompanying materials are made available under the terms of the Eclipse Public License 2.0
 * which is available at https://www.eclipse.org/legal/epl-2.0/
 * SPDX-License-Identifier: EPL-2.0
 * 
 * Created, in part, with funding and support from the United States Government. (see Acknowledgments file).
 * 
 * This program includes and/or can make use of certain third party source code, object code, documentation and other
 * files ("Third Party Software"). The Third Party Software that is used by this program is dependent upon your system
 * configuration. By using this program, You agree to comply with any and all relevant Third Party Software terms and
 * conditions contained in any such Third Party Software or separate license file distributed with such Third Party
 * Software. The parties who own the Third Party Software ("Third Party Licensors") are intended third party benefici-
 * aries to this license with respect to the terms applicable to their Third Party Software. Third Party Software li-
 * censes only apply to the Third Party Software and not any other portion of this program or this program as a whole.
 */
import java.util.List
import org.eclipse.emf.common.util.EList
import org.osate.aadl2.NumberType
import org.osate.aadl2.Property
import org.osate.aadl2.PropertyExpression
import org.osate.aadl2.PropertyType
import org.osate.aadl2.UnitsType
import org.osate.alisa.common.typing.CommonTypeSystem
import org.osate.verify.verify.ComputeRef
import org.osate.verify.verify.FormalParameter
import org.osate.verify.verify.VerificationActivity
import org.osate.verify.verify.VerifyPackage

system org.osate.verify.typing.VerifyTypeSystem extends CommonTypeSystem

// can't do this!  validatorExtends AbstractVerifyValidator
// instead copied getPackages into VerifyValidator

judgments {
	assignArgument |- FormalParameter formal <- PropertyExpression actual
	assignReturn |- FormalParameter ret --> ComputeRef compute
	assignArguments |- VerificationActivity owner ~> List<FormalParameter> formals << List<PropertyExpression> actuals
	assignReturns |- VerificationActivity owner ~> List<FormalParameter> returns >> List<ComputeRef> computes
	assignProperties ||- VerificationActivity owner ~> List<Property> properties << List<PropertyExpression> values
}

rule TypeFormalParameter
	G |- FormalParameter param : PropertyType expType
from {
	val declared = param.getType
	declared !== null
	G |- declared : expType
	if (param.unit !== null) {
		val targetType = param.unit.eContainer as UnitsType
		expType instanceof NumberType
		or
			fail
				error 'typing: can only convert unit of a numeric type'
				source param
		val numType = expType as NumberType
		numType.unitsType !== null
		or
			fail
				error 'typing: parameter type has no unit'
				source param
		G |- numType.unitsType ~~ targetType
		or
			fail
				error 'typing: cannot convert ' + stringRep(numType) + ' to ' + stringRep(targetType)
				source param
	}
}

rule ArgumentSequence
  G |- VerificationActivity owner ~> List<FormalParameter> formals << EList<PropertyExpression> actuals
from {
  actuals.size == formals.size
  or
  	fail
    	error 'expected ' + formals.size + ' arguments, but got ' + actuals.size
    	source owner 
    
  val aIter = actuals.iterator
  val fIter = formals.iterator
  while (aIter.hasNext && fIter.hasNext) {
  	val actual = aIter.next
  	val formal = fIter.next
    G |- formal <- actual
  }
}

rule Argument
  G |- FormalParameter formal <- PropertyExpression actual
from {
	G |- actual : var PropertyType actualType
	var PropertyType formalType
	G |- formal : formalType
	or {/* ignore */}
	if (formalType !== null) {
		G |- formalType <: actualType
  	or
    	fail
	  		error 'typing: ' + stringRep(actualType) + 'cannot be assigned to ' + stringRep(formalType)
	  		source actual
	}
}

rule ReturnSequence
  G |- VerificationActivity owner ~> List<FormalParameter> returns >> List<ComputeRef> computeRefs
from {
  computeRefs.size == returns.size
  or
  	fail
    	error 'expected ' + returns.size + ' return values, but got ' + computeRefs.size
    	feature VerifyPackage.eINSTANCE.verificationActivity_Computes
    	source owner 
    
  val cIter = computeRefs.iterator
  val rIter = returns.iterator
  while (cIter.hasNext && rIter.hasNext) {
  	val computeRef = cIter.next
  	val ret = rIter.next
    G |- ret --> computeRef
  }
}

rule Return
  G |- FormalParameter ret --> ComputeRef computeRef
from {
	if (computeRef.compute !== null) {
	G |- computeRef.compute : var PropertyType computeType
	var PropertyType retType
	G |- ret : retType
	or {/* ignore */}
	if (retType !== null) {
		G |- computeType <: retType
  	or
    	fail
	  		error 'typing: ' + stringRep(retType) + 'cannot be assigned to ' + stringRep(computeType)
	  		source computeRef
  }
  }
}

rule PropertyValues
	G ||- VerificationActivity owner ~> List<Property> properties << List<PropertyExpression> values
from {
  properties.size == values.size
  or
  	fail
    	error 'expected ' + properties.size + ' property values, but got ' + values.size
    	feature VerifyPackage.eINSTANCE.verificationActivity_PropertyValues
    	source owner 
    
  val pIter = properties.iterator
  val vIter = values.iterator
  while (pIter.hasNext && vIter.hasNext) {
  	val property = pIter.next
  	val value = vIter.next
  	val left = property.propertyType
  	G |- value : var PropertyType right
		G |- left <: right
	 	or
  	 	fail
  			error 'typing: ' + stringRep(right) + 'cannot be assigned to ' + stringRep(left)
  			source value
  }
}

checkrule CheckVerificationMethodParams for
	FormalParameter formal
from {
	empty |-formal : var PropertyType type
}

checkrule CheckActivityArguments for 
	VerificationActivity activity 
from {
	val method = activity.method
	empty |- activity ~> method.formals << activity.actuals
}

checkrule CheckActivityReturns for 
	VerificationActivity activity 
from {
	val method = activity.method
	empty |- activity ~> method.results >> activity.computes
}

checkrule CheckPropertyTypes for
	VerificationActivity activity
from {
	val method = activity.method
	empty ||- activity ~> method.properties << activity.propertyValues
}
