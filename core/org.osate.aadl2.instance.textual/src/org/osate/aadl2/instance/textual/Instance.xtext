/**
 * Copyright (c) 2004-2020 Carnegie Mellon University and others. (see Contributors file). 
 * All Rights Reserved.
 * 
 * NO WARRANTY. ALL MATERIAL IS FURNISHED ON AN "AS-IS" BASIS. CARNEGIE MELLON UNIVERSITY MAKES NO WARRANTIES OF ANY
 * KIND, EITHER EXPRESSED OR IMPLIED, AS TO ANY MATTER INCLUDING, BUT NOT LIMITED TO, WARRANTY OF FITNESS FOR PURPOSE
 * OR MERCHANTABILITY, EXCLUSIVITY, OR RESULTS OBTAINED FROM USE OF THE MATERIAL. CARNEGIE MELLON UNIVERSITY DOES NOT
 * MAKE ANY WARRANTY OF ANY KIND WITH RESPECT TO FREEDOM FROM PATENT, TRADEMARK, OR COPYRIGHT INFRINGEMENT.
 * 
 * This program and the accompanying materials are made available under the terms of the Eclipse Public License 2.0
 * which is available at https://www.eclipse.org/legal/epl-2.0/
 * SPDX-License-Identifier: EPL-2.0
 * 
 * Created, in part, with funding and support from the United States Government. (see Acknowledgments file).
 * 
 * This program includes and/or can make use of certain third party source code, object code, documentation and other
 * files ("Third Party Software"). The Third Party Software that is used by this program is dependent upon your system
 * configuration. By using this program, You agree to comply with any and all relevant Third Party Software terms and
 * conditions contained in any such Third Party Software or separate license file distributed with such Third Party
 * Software. The parties who own the Third Party Software ("Third Party Licensors") are intended third party benefici-
 * aries to this license with respect to the terms applicable to their Third Party Software. Third Party Software li-
 * censes only apply to the Third Party Software and not any other portion of this program or this program as a whole.
 */
grammar org.osate.aadl2.instance.textual.Instance with org.osate.xtext.aadl2.properties.Properties

import "http://aadl.info/AADL/2.0/instance" as instance
import "http://aadl.info/AADL/2.0" as aadl2

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

SystemInstance returns instance::SystemInstance:
	category=ComponentCategory name=ID ':' componentImplementation=[aadl2::ComponentImplementation|ImplRef] '{' (
		featureInstance+=FeatureInstance |
		componentInstance+=ComponentInstance |
		connectionInstance+=ConnectionInstance |
		flowSpecification+=FlowSpecificationInstance |
		endToEndFlow+=EndToEndFlowInstance |
		modeInstance+=ModeInstance |
		modeTransitionInstance+=ModeTransitionInstance |
		systemOperationMode+=SystemOperationMode |
		ownedPropertyAssociation+=PropertyAssociationInstance
	)* '}'
;

FeatureInstance returns instance::FeatureInstance:
	direction=DirectionType category=FeatureCategory name=ID ('[' index=Long ']')? ':' feature=[aadl2::Feature|DeclarativeRef] ('{' (
		featureInstance+=FeatureInstance |
		ownedPropertyAssociation+=PropertyAssociationInstance
	)* '}')?
;
	
ComponentInstance returns instance::ComponentInstance:
	category=ComponentCategory classifier=[aadl2::ComponentClassifier|ClassifierRef]? name=ID ('[' index+=Long ']')*
	('in' 'modes' '(' inMode+=[instance::ModeInstance] (',' inMode+=[instance::ModeInstance])*')')?
	':' subcomponent=[aadl2::Subcomponent|DeclarativeRef] ('{' (
		featureInstance+=FeatureInstance |
		componentInstance+=ComponentInstance |
		connectionInstance+=ConnectionInstance |
		flowSpecification+=FlowSpecificationInstance |
		endToEndFlow+=EndToEndFlowInstance |
		modeInstance+=ModeInstance |
		modeTransitionInstance+=ModeTransitionInstance |
		ownedPropertyAssociation+=PropertyAssociationInstance
	)* '}')?
;

ConnectionInstance returns instance::ConnectionInstance:
	complete?='complete'? kind=ConnectionKind name=STRING ':'
	source=[instance::ConnectionInstanceEnd|InstanceRef] (bidirectional?='<->' | '->') destination=[instance::ConnectionInstanceEnd|InstanceRef]
	('in' 'modes' '(' inSystemOperationMode+=[instance::SystemOperationMode] (',' inSystemOperationMode+=[instance::SystemOperationMode])* ')')?
	('in' 'transitions' '(' inModeTransition+=[instance::ModeTransitionInstance|TransitionRef] (',' inModeTransition+=[instance::ModeTransitionInstance|TransitionRef])* ')')? '{' (
		connectionReference+=ConnectionReference |
		ownedPropertyAssociation+=PropertyAssociationInstance
	)+ '}'
;

ConnectionReference returns instance::ConnectionReference:
	source=[instance::ConnectionInstanceEnd|InstanceRef] '->' destination=[instance::ConnectionInstanceEnd|InstanceRef] ':'
	(reverse?='reverse')? connection=[aadl2::Connection|DeclarativeRef] 'in' context=[instance::ComponentInstance|InstanceRef]
;

FlowSpecificationInstance returns instance::FlowSpecificationInstance:
	'flow' name=ID '(' source=[instance::FeatureInstance|InstanceRef]? '->' destination=[instance::FeatureInstance|InstanceRef]? ')'
	('in' 'modes' '(' inMode+=[instance::ModeInstance] (',' inMode+=[instance::ModeInstance])*')')?
	('in' 'transitions' '(' inModeTransition+=[instance::ModeTransitionInstance|TransitionRef] (',' inModeTransition+=[instance::ModeTransitionInstance|TransitionRef])* ')')?
	':' flowSpecification=[aadl2::FlowSpecification|DeclarativeRef] ('{' ownedPropertyAssociation+=PropertyAssociationInstance+ '}')?
;

EndToEndFlowInstance returns instance::EndToEndFlowInstance:
	'end' 'to' 'end' 'flow' name=ID (flowElement+=[instance::FlowElementInstance|InstanceRef] ('->' flowElement+=[instance::FlowElementInstance|InstanceRef])*)?
	('in' 'modes' '(' inSystemOperationMode+=[instance::SystemOperationMode] (',' inSystemOperationMode+=[instance::SystemOperationMode])* ')')?
	':' endToEndFlow=[aadl2::EndToEndFlow|DeclarativeRef] ('{' ownedPropertyAssociation+=PropertyAssociationInstance '}')?
;

ModeInstance returns instance::ModeInstance:
	initial?='initial'? derived?='derived'? 'mode' name=ID
	('=' (parent+=[instance::ModeInstance] | ('(' parent+=[instance::ModeInstance] (',' parent+=[instance::ModeInstance])+ ')')))?
	':' mode=[aadl2::Mode|DeclarativeRef] ('{' ownedPropertyAssociation+=PropertyAssociationInstance '}')?
;

ModeTransitionInstance returns instance::ModeTransitionInstance:
	'mode' 'transition' name=ID source=[instance::ModeInstance]
	'-[' (trigger+=[instance::FeatureInstance|InstanceRef] (',' trigger+=[instance::FeatureInstance|InstanceRef])*)? ']->'
	destination=[instance::ModeInstance]
	':' modeTransition=[aadl2::ModeTransition|DeclarativeRef] ('{' ownedPropertyAssociation+=PropertyAssociationInstance '}')?
;

SystemOperationMode returns instance::SystemOperationMode:
	'som' name=STRING (currentMode+=[instance::ModeInstance|InstanceRef] (',' currentMode+=[instance::ModeInstance|InstanceRef])*)?
;

PropertyAssociationInstance returns instance::PropertyAssociationInstance:
	property=[aadl2::Property|QPREF] '=>' ownedValue+=OptionalModalPropertyValue (',' ownedValue+=OptionalModalPropertyValue)*
	':' propertyAssociation=[aadl2::PropertyAssociation|PropertyAssociationRef]
;

@Override 
OptionalModalPropertyValue returns aadl2::ModalPropertyValue:
	ownedValue=PropertyExpression ('in' 'modes' '(' inMode+=[aadl2::Mode] (',' inMode+=[aadl2::Mode])* ')')?
;

@Override 
PropertyExpression returns aadl2::PropertyExpression:
	RecordTerm | ReferenceTerm | InstanceReferenceValue | ComponentClassifierTerm | ComputedTerm | StringTerm |
	NumericRangeTerm | RealTerm | IntegerTerm | ListTerm | BooleanLiteral | LiteralorReferenceTerm
;

@Override 
ContainmentPathElement returns aadl2::ContainmentPathElement:
	namedElement=[aadl2::NamedElement|DeclarativeRef] (arrayRange+=ArrayRange)? ('/' path=ContainmentPathElement)?
;

InstanceReferenceValue returns instance::InstanceReferenceValue:
	'reference' '(' referencedInstanceObject=[instance::InstanceObject|InstanceRef] ')'
;


DirectionType returns aadl2::DirectionType:
	'in' | 'out' | 'in' 'out'
;

FeatureCategory returns instance::FeatureCategory:
	'dataPort' | 'eventPort' | 'eventDataPort' | 'parameter' |
	'busAccess' | 'dataAccess'| 'subprogramAccess' | 'subprogramGroupAccess' |
	'featureGroup' | 'abstractFeature'
;

ComponentCategory returns aadl2::ComponentCategory: 'abstract' | 'bus'|'data'
	| 'device' | 'memory' | 'process' | 'processor' | 'subprogram'
	| 'subprogram' 'group' | 'system' | 'thread' 'group'
	| 'thread' | 'virtual' 'bus' | 'virtual' 'processor';

ConnectionKind returns instance::ConnectionKind:
	'featureConnection' | 'accessConnection' | 'parameterConnection' |
	'portConnection' | 'featureGroupConnection'
;

ClassifierRef:
	(ID '::')+ ID ('.' ID)?
;

ImplRef:
	(ID '::')+ ID '.' ID
;

DeclarativeRef:
	(ID '::')+ ID ('.' ID)? ':' ('transition' '#' INTEGER_LIT | ID)
;

InstanceRef:
	'parent' | (ID ('[' INTEGER_LIT ']')* '.')* ('connection' '#' INTEGER_LIT | ID ('[' INTEGER_LIT ']')*)
;

TransitionRef:
	'transition' '#' INTEGER_LIT
;

PropertyAssociationRef:
	(ID '::')+ ID ('.' ID)? ':' ((ID | 'transition' '#' INTEGER_LIT)':')? 'property' '#' INTEGER_LIT
;

Long returns ecore::ELong:
	INTEGER_LIT
;